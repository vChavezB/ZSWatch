cmake_minimum_required(VERSION 3.20.0)

string(REGEX MATCH "zswatch_nrf5340_cpuapp(_ns)?@([0-9]+)$" ZSWATCH_BOARD "${BOARD}")
if(ZSWATCH_BOARD )
    set(ZSWATCH_REV ${CMAKE_MATCH_2})
    if(${ZSWATCH_REV} GREATER_EQUAL 2)
        set(DFU_BUILD true)
        set(PM_STATIC_YML_FILE ${CMAKE_CURRENT_SOURCE_DIR}/partition.yml)
        set(OVERLAY_CONFIG ${CMAKE_CURRENT_SOURCE_DIR}/boards/dfu.conf)
        set(mcuboot_DTC_OVERLAY_FILE ${CMAKE_CURRENT_SOURCE_DIR}/overlays/mcuboot.overlay)       
    endif()
endif()

set(BOARD_ROOT ${CMAKE_CURRENT_SOURCE_DIR})
find_package(Zephyr REQUIRED HINTS $ENV{ZEPHYR_BASE})
project(ZSWatchFw)

add_subdirectory(drivers)
add_subdirectory(src/applications)
add_subdirectory(src/ext_drivers/bmi270)
add_subdirectory(src/ext_drivers/bmp581)

include_directories(src/)
include_directories(src/ui)
include_directories(src/applications)

target_sources(app PRIVATE src/battery.c)
target_sources(app PRIVATE src/ble_aoa.c)
target_sources(app PRIVATE src/ble_comm.c)
target_sources(app PRIVATE src/ble_transport.c)
target_sources(app PRIVATE src/buttons.c)
target_sources(app PRIVATE src/display_control.c)
target_sources(app PRIVATE src/gpio_debug.c)
target_sources(app PRIVATE src/heart_rate_sensor.c)
target_sources(app PRIVATE src/main.c)
target_sources(app PRIVATE src/notification_manager.c)
target_sources(app PRIVATE src/ram_retention_storage.c)
target_sources(app PRIVATE src/vibration_motor.c)
target_sources(app PRIVATE src/watchface_app.c)
target_sources(app PRIVATE src/zsw_battery_manager.c)
target_sources(app PRIVATE src/zsw_clock.c)
target_sources(app PRIVATE src/zsw_charger.c)
target_sources(app PRIVATE src/zsw_cpu_freq.c)
target_sources(app PRIVATE src/zsw_env_sensor.c)
target_sources(app PRIVATE src/zsw_gatt_sensor_server.c)
target_sources(app PRIVATE src/zsw_imu.c)
target_sources(app PRIVATE src/zsw_magnetometer.c)
target_sources(app PRIVATE src/zsw_periodic_event.c)
target_sources(app PRIVATE src/zsw_phone_app_publisher.c)
target_sources(app PRIVATE src/zsw_power_manager.c)
target_sources(app PRIVATE src/zsw_pressure_sensor.c)

if (DFU_BUILD)
    target_sources(app PRIVATE src/dfu.c)
endif()

FILE(GLOB img_sources src/images/*.c)
target_sources(app PRIVATE ${img_sources})

FILE(GLOB font_sources src/images/fonts/*.c)
target_sources(app PRIVATE ${font_sources})

FILE(GLOB ui_sources src/ui/*.c)
target_sources(app PRIVATE ${ui_sources})
zephyr_sources_ifdef(CONFIG_WATCHFACE_ANALOG src/ui/watchfaces/zsw_watchface_analog_ui.c)
zephyr_sources_ifdef(CONFIG_WATCHFACE_DIGITAL src/ui/watchfaces/zsw_watchface_digital_ui.c)
zephyr_sources_ifdef(CONFIG_WATCHFACE_MINIMAL src/ui/watchfaces/zsw_watchface_minimal_ui.c)


FILE(GLOB events_sources src/events/*.c)
target_sources(app PRIVATE ${events_sources})

add_compile_definitions(LV_LVGL_H_INCLUDE_SIMPLE)

# BME688 BSEC implementation
set(bme688_bsec_lib_path ${CMAKE_CURRENT_SOURCE_DIR}/lib/bme688_bsec)
target_link_libraries(app PRIVATE ${bme688_bsec_lib_path}/bin/gcc/Cortex_M33/libalgobsec.a)
target_include_directories(app PRIVATE ${bme688_bsec_lib_path}/inc)
target_sources(app PRIVATE src/bme688_bsec/bme688_bsec.c)